#Область ОписаниеПеременных

// Модуль настрооек синтаксиса
&НаКлиенте
Перем Синтаксис;

// Модуль парсинга текста
&НаКлиенте
Перем Парсер;

// Модуль определителя имен реквизитов и элементов
&НаКлиенте
Перем ОпределительРеквизитов;

// Модуль форматирования текста
&НаКлиенте
Перем Форматтер;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	СведенияОВнешнейОбработке = ОбработкаОбъект.СведенияОВнешнейОбработке();	
	
	Версия = СведенияОВнешнейОбработке.Версия;
	
	ЭтаФорма.Заголовок = СтрШаблон("MDDesign. Версия %1", Версия);
	
	ЭтаФорма.__ИдентификаторОбработки = ОбработкаОбъект.Метаданные().ПолноеИмя();	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	// BSLLS:GetFormMethod-off
	Парсер = ПолучитьФорму(__ИдентификаторОбработки + ".Форма.Парсер", , ЭтаФорма);
	ОпределительРеквизитов = ПолучитьФорму(__ИдентификаторОбработки + ".Форма.ОпределительРеквизитов", , ЭтаФорма);
	Синтаксис = ПолучитьФорму(__ИдентификаторОбработки + ".Форма.Синтаксис", , ЭтаФорма);
	Форматтер = ПолучитьФорму(__ИдентификаторОбработки + ".Форма.Форматтер", , ЭтаФорма);
	// BSLLS:GetFormMethod-on
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура __ПостроитьФорму(Команда)
	ФормаГруппы = Парсер().Разобрать(__Редактор);
	ОписанияРеквизитов = ОпределительРеквизитов().Заполнить(ФормаГруппы); 
	
	Построить(ФормаГруппы, ОписанияРеквизитов);
	
	ЗаголовокФормы = ФормаГруппы.НаборСвойств.Заголовок;
	Если ПустаяСтрока(ЗаголовокФормы) Тогда
		ЗаголовокФормы = "<Без заголовка>";
	КонецЕсли;
	
	ЭтотОбъект.__ЗаголовокФормы = ЗаголовокФормы;
КонецПроцедуры

&НаКлиенте
Процедура __ПостроитьФормуВОтдельномОкне(Команда)    
	ФормаГруппы = Парсер().Разобрать(__Редактор);
	ОписанияРеквизитов = ОпределительРеквизитов().Заполнить(ФормаГруппы); 
	
	ФормаПросмотр = ОткрытьФорму(__ИдентификаторОбработки + ".Форма.ФормаПросмотр", , ЭтаФорма);
	ФормаПросмотр.Построить(ФормаГруппы, ОписанияРеквизитов);
КонецПроцедуры

&НаКлиенте
Процедура __Форматировать(Команда)
	ФормаГруппы = Парсер().Разобрать(__Редактор);
	ЭтаФорма.__Редактор = Форматтер().Форматировать(ФормаГруппы);
	ЭтотОбъект.ТекущийЭлемент = Элементы.__Редактор;
КонецПроцедуры

&НаКлиенте
Процедура __ДобавитьКартинку(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборКартинкиЗавершение", ЭтотОбъект);
						 
	ОткрытьФорму(__ИдентификаторОбработки + ".Форма.ФормаВыбораКартинки", , ЭтаФорма, , , , ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура ВыборКартинкиЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.__Редактор.ВыделенныйТекст = Лексема("Картинка") + РезультатЗакрытия;
КонецПроцедуры

&НаКлиенте
Процедура __РедакторТаблицы(Команда)                               
	НачалоСтроки = 0;
	НачалоКолонки = 0;
	КонецСтроки = 0;
	КонецКолонки = 0;
	
	Элементы.__Редактор.ПолучитьГраницыВыделения(НачалоСтроки, НачалоКолонки, КонецСтроки, КонецКолонки);

	ФормаГруппы = Парсер().Разобрать(__Редактор);
	
	ИерархияЭлементов = Новый Массив;
	
	ПолучитьИерархиюЭлементовПоКоординатам(ИерархияЭлементов, ФормаГруппы, НачалоСтроки, НачалоКолонки);
	
	Если ИерархияЭлементов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущееОписание = ИерархияЭлементов[ИерархияЭлементов.ВГраница()];
	
	Если ТекущееОписание.Тип <> "Таблица"
		И ТекущееОписание.Тип <> "Дерево" Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ОписаниеТаблицы", ТекущееОписание);
	
	ДополнительныеПараметры = Новый Структура("ИерархияЭлементов", ИерархияЭлементов);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("РедактированиеТаблицыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму(__ИдентификаторОбработки + ".Форма.ФормаРедактированияТаблицы", ПараметрыОткрытия, ЭтотОбъект, , , , ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеТаблицыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИерархияЭлементов = ДополнительныеПараметры.ИерархияЭлементов;

	ТекущийРодитель = ИерархияЭлементов[ИерархияЭлементов.ВГраница() - 1];
	ТекущееОписание = ИерархияЭлементов[ИерархияЭлементов.ВГраница()];
	
	ИндексЭлемента =  ТекущийРодитель.Элементы.Найти(ТекущееОписание);
	
	ТекущийРодитель.Элементы[ИндексЭлемента] = РезультатЗакрытия;
	
	ЭтаФорма.__Редактор = Форматтер().Форматировать(ИерархияЭлементов[0]);
КонецПроцедуры
 
&НаКлиенте
Процедура __КомандаЗаглушка(Команда)
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = "Заглушка";
	Сообщение.Сообщить();
КонецПроцедуры

&НаКлиенте
Процедура __ДобавитьПрямыеСкобки(Команда)
	ОбернутьВыделенныйТекст("[]", "");
КонецПроцедуры

&НаКлиенте
Процедура __ДобавитьФигурныеСкобки(Команда)
	ОбернутьВыделенныйТекст("{", "}");
КонецПроцедуры

&НаКлиенте
Процедура __ДобавитьУгловыеСкобки(Команда)
	ОбернутьВыделенныйТекст("<", ">");
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПолучитьИерархиюЭлементовПоКоординатам(ИерархияЭлементов, ОписаниеЭлемента, Верх, Лево)
	Интервалы = ОписаниеЭлемента.Координаты.Получить(Верх);
	
	Если Интервалы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Интервал Из Интервалы Цикл
		ВходитВИнтервал = Лево >= Интервал.Лево И Лево <= Интервал.Право;
		Если НЕ ВходитВИнтервал Тогда
			Продолжить;
		КонецЕсли;
		
		ИерархияЭлементов.Добавить(ОписаниеЭлемента); 
		Прервать;
	КонецЦикла;
	
	Если НЕ ВходитВИнтервал Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ОписаниеЭлемента.Свойство("Элементы") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Подэлемент Из ОписаниеЭлемента.Элементы Цикл
		ПолучитьИерархиюЭлементовПоКоординатам(ИерархияЭлементов, Подэлемент, Верх, Лево);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция Парсер() Экспорт
	Возврат Парсер;
КонецФункции

&НаКлиенте
Функция Синтаксис() Экспорт
	Возврат Синтаксис;
КонецФункции

&НаКлиенте
Функция Форматтер() Экспорт
	Возврат Форматтер;
КонецФункции     

&НаКлиенте
Функция ОпределительРеквизитов() Экспорт
	Возврат ОпределительРеквизитов;
КонецФункции     

&НаКлиенте
Функция Лексема(ИмяПараметра) Экспорт
	Возврат Синтаксис().Лексема(ИмяПараметра);
КонецФункции

&НаКлиенте
Процедура СообщитьОбОшибкеПарсинга(Строка, Столбец, Сообщение, Отказ)
	Отказ = Истина;
	ТекстСообщения = СтрШаблон("%1 [%2, %3]", Сообщение, Строка, Столбец);
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщения;
	Сообщение.Сообщить();  
	
	Элементы.__Редактор.УстановитьГраницыВыделения(Строка, Столбец, Строка, Столбец + 1);
КонецПроцедуры

&НаКлиенте
Функция ЗначениеВМассиве(Знач Значение)
	Результат = Новый Массив;
	Результат.Добавить(Значение);
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура Построить(ОписаниеГруппы, ОписанияРеквизитов) Экспорт
	ПостроитьНаСервере(ОписаниеГруппы, ОписанияРеквизитов);
КонецПроцедуры

&НаСервере
Процедура ПостроитьНаСервере(Знач ОписаниеГруппы, Знач ОписанияРеквизитов) Экспорт
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ПостроитьНаСервере(
		ЭтотОбъект, 
		Элементы.__ГруппаПросмотр, 
		ОписаниеГруппы, 
		ОписанияРеквизитов);
КонецПроцедуры

&НаКлиенте
Процедура ОбернутьВыделенныйТекст(СимволДо, СимволПосле)
	НачалоСтроки = 0;
	НачалоКолонки = 0;
	КонецСтроки = 0;
	КонецКолонки = 0;
	
	Элементы.__Редактор.ПолучитьГраницыВыделения(НачалоСтроки, НачалоКолонки, КонецСтроки, КонецКолонки);
	Элементы.__Редактор.ВыделенныйТекст = СимволДо + Элементы.__Редактор.ВыделенныйТекст + СимволПосле;
	Элементы.__Редактор.УстановитьГраницыВыделения(НачалоСтроки, НачалоКолонки + СтрДлина(СимволДо), КонецСтроки, КонецКолонки + СтрДлина(СимволДо));
	
	ЭтотОбъект.ТекущийЭлемент = Элементы.__Редактор;
КонецПроцедуры

#КонецОбласти  

